buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.5.+'
    }
}
apply plugin: 'android'

configurations {
    apt
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext.androidAnnotationsVersion = '3.0-SNAPSHOT'

dependencies {
    compile "com.android.support:support-v4:13.0.0"
    compile "org.springframework.android:spring-android-rest-template:1.0.1.RELEASE"
    compile "com.fasterxml.jackson.core:jackson-databind:2.2.2"
	compile "org.androidannotations:androidannotations-api:${androidAnnotationsVersion}"
	compile "com.j256.ormlite:ormlite-android:4.45"
    apt "org.androidannotations:androidannotations:${androidAnnotationsVersion}"
}

android {
    compileSdkVersion 17
    buildToolsVersion "17.0.0"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 17
    }

    applicationVariants.each { variant ->
        variant.javaCompile.doFirst {
            aptOutput = file("${project.buildDir}/source/apt/${variant.dirName}")
            aptOutput.mkdirs()
            variant.javaCompile.options.compilerArgs += [
                    '-processorpath', configurations.apt.getAsPath(),
                    '-AandroidManifestFile=' + variant.processResources.manifestFile,
                    '-s', aptOutput
            ]
        }
    }
}

task launchDebug(dependsOn: 'installDebug') << {
	"adb shell am start -n fr.ydelouis.selfoss/.activity.MainActivity_".execute()
}
